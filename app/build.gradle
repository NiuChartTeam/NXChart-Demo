apply plugin: 'com.android.application'
repositories {
    maven { url "https://raw.githubusercontent.com/NiuChartTeam/maven-repo-niuchart/master" }
}
def versionPropsFile = file('version.properties')
def Properties versionProps = new Properties()
versionProps.load(new FileInputStream(versionPropsFile))
def gitVersionCode = versionProps['versionCode'].toInteger()
def gitVersionName = versionProps['versionName']
def gitTargetSdkVersion = versionProps['targetSdkVersion'].toInteger()
def gitMinSdkVersion = versionProps['minSdkVersion'].toInteger()
def gitCompileSdkVersion = versionProps['compileSdkVersion'].toInteger()

android {
    compileSdkVersion gitCompileSdkVersion
    buildToolsVersion '23.0.2'
    defaultConfig {
        applicationId "com.niuchart.demo"
        minSdkVersion gitMinSdkVersion
        targetSdkVersion gitTargetSdkVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            zipAlignEnabled true
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def apkName = "NiuChart-Demo-"+gitVersionName+".apk";
                    def file = output.outputFile;
                    output.outputFile = new File(file.parent, apkName)
                }

            }
        }
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dexOptions {
        incremental true
    }
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/maven/com.belerweb/pinyin4j/pom.xml'
        exclude 'META-INF/maven/com.belerweb/pinyin4j/pom.properties'
        exclude 'LICENSE.txt'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    productFlavors {
        betaConfig {
            versionName 'dev-debug'
            versionCode gitVersionCode
        }
        releaseConfig {
            versionCode gitVersionCode
            versionName gitVersionName
        }
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile 'com.android.support:support-v4:23.0.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile('com.niuchart:NXCore:'+gitVersionName+'@aar') {
            transitive = true;
    }
}
